task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 7

repositories {
    mavenCentral()
}

configurations {
    provided
    provided.extendsFrom(compile)
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

dependencies {
    //compile group: 'org.mvel', name: 'mvel2', version:'2.1.4.Final'
    provided group: 'io.dropwizard', name: 'dropwizard-core', version: '0.8.2'
    provided group: 'io.dropwizard', name: 'dropwizard-auth', version: '0.8.2'
    testCompile group:'org.codehaus.groovy', name:'groovy-all', version:'2.4.3'
    testCompile group:'org.spockframework', name:'spock-core', version:'1.0-groovy-2.4'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.8.2'
    testCompile (group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-grizzly2', version: '2.19') {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'junit', module: 'junit'
    }
}

apply plugin: 'idea'

idea {
    module {
        scopes.PROVIDED.plus += [project.configurations.provided]
    }
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.stansvec'
            version = '0.1.1'
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url 'https://api.bintray.com/maven/stansvec/maven/dropwizard-authorization'
            credentials {
                username 'stansvec'
                password 'Insert API key here'
            }
        }
    }
}
